include "../core_timed_defs.csp"

channel terminate
	
channel followFlightPlan: SearchType
channel WatchForAbnormalities: Bool
channel recordVideo

channel setwAbnormalities: Bool
channel getwAbnormalities: Bool

channel setSearchType: SearchType
channel getSearchType: SearchType

datatype SearchType = SearchType_AreaSearch | SearchType_RapidParallelLine | SearchType_Linear | SearchType_CreepingLine | SearchType_PointOfInterest | SearchType_MountainFace | SearchType_WoodedArea | SearchType_Avalanche


Timed(OneStep) {
	
	
	DeadlineProcess = timed_priority(((Pilot [| diff(inter(A_Pilot,A_Handheld),gets) |] Handheld) [| diff(inter(union(A_Pilot, A_Handheld),A_Observer),gets) |] Observer)[| union(sharedVars, {terminate}) |] varMemory) 

	Pilot = getwAbnormalities?wAbnormalities -> WatchForAbnormalities!wAbnormalities -> Deadline(dead_1, 5); terminate -> SKIP
	
	dead_1 = followFlightPlan!SearchType_Linear->wait(1)
	
	Observer = Deadline(dead_2, 3)
	
	dead_2 = recordVideo -> SKIP; terminate -> SKIP
	
	Handheld = recordVideo -> SKIP; followFlightPlan?searchType -> (setSearchType!searchType -> SKIP); terminate -> SKIP
	
	varMemory = Memory_searchType(SearchType_AreaSearch)
					 [| { terminate } |]
					 Memory_wAbnormalities(false)
					-- [| { terminate } |] (
					-- Memory_asearchType(SearchType_AreaSearch)
					-- )
					-- )

	Memory_searchType(searchType) =
				getSearchType!searchType -> Memory_searchType(searchType)
				[]
				setSearchType?x__ -> Memory_searchType(x__)
				[]
				terminate -> SKIP
				
				
	Memory_wAbnormalities(wAbnormalities) =
				getwAbnormalities!wAbnormalities -> Memory_wAbnormalities(wAbnormalities)
				[]
				setwAbnormalities?x__ -> Memory_wAbnormalities(x__)
				[]
				terminate -> SKIP
				
	ALL = {|WatchForAbnormalities, followFlightPlan,recordVideo,terminate,setSearchType,getSearchType,getwAbnormalities,setwAbnormalities|}
	
	
	
	TRUN(S)=RUN(union(S, {tock}))
	seconds(t)=t
	s(t) = seconds(t)

	ADeadline(S,E,d) = EndBy(TRUN(S),d) [|E|> SKIP
	
	prop1 = timed_priority(
		ADeadline(
			ALL,{|terminate|},s(10)
		)
		)
}

A_Pilot = union(union({|WatchForAbnormalities, followFlightPlan,terminate|},searchTypeVars),wAbnormalitiesVars)

A_Handheld = {|recordVideo, followFlightPlan, terminate|} 

A_Observer = {|recordVideo, terminate|}

sharedVars = union(searchTypeVars,wAbnormalitiesVars)

searchTypeVars = {|setSearchType,getSearchType|}
wAbnormalitiesVars = {|getwAbnormalities,setwAbnormalities|}

gets = {|getSearchType,getwAbnormalities|}

assert not DeadlineProcess :[deadlock free]
assert DeadlineProcess :[divergence free]
assert DeadlineProcess :[deterministic]

assert DeadlineProcess :[has trace [T]]: <getwAbnormalities.false,WatchForAbnormalities.false,recordVideo,
followFlightPlan.SearchType_Linear,setSearchType.SearchType_Linear,tock,terminate>

assert DeadlineProcess :[has trace [T]]: <getwAbnormalities.false,recordVideo,WatchForAbnormalities.false,
followFlightPlan.SearchType_Linear,tock,setSearchType.SearchType_Linear,terminate>



